buildscript {
	ext {
		springBootVersion = '1.5.8.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "nebula.ospackage" version "4.5.1"
}

ospackage {
	packageName = 'bahmni-reporting'
	release = System.getenv('GO_PIPELINE_COUNTER') ?: 1
	arch = NOARCH
	os = LINUX
	user = 'root'

	into '/opt/bahmni-reporting'

	from("${projectDir}/scripts/rpm/") {
		fileMode = 0755
		createDirectoryEntry = true
		into '/opt/bahmni-reporting/bin'
	}

	from("${buildDir}/libs") {
		fileMode = 0755
		createDirectoryEntry = true
		into "/opt/bahmni-reporting/libs"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.bahmni.batch'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile("org.springframework.boot:spring-boot-starter-batch")
	compile("org.springframework.batch:spring-batch-test")
	compile("org.hsqldb:hsqldb")

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

buildRpm {
	dependsOn "build", "jar"

	requires("cronie")
	requires('postgresql92')

	postInstall file("${projectDir}/scripts/postinstall.sh")
	postUninstall file("${projectDir}/scripts/postuninstall.sh")
}
